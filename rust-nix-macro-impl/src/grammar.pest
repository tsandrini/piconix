WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT    = _{ "#" ~ (!"\n" ~ ANY)* }

integer    = @{ "-"? ~ ASCII_DIGIT+ }
identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_" | "-")* }

expr = { list | attrset | string | integer | identifier }

interpolation = { "${" ~ expr ~ "}" }

escaped_quote = @{ "\\" ~ "\"" }
escaped_interpolation = @{ "''${" }

string_literal_part = @{ ( !("\"" | "$" | "\\" | "'") ~ ANY )+ }
dollar_literal = @{ "$" ~ !("{") }
single_quote_literal = @{ "'" ~ !"'" }

string_content = {
    escaped_quote |
    escaped_interpolation |
    interpolation |
    string_literal_part |
    dollar_literal |
    single_quote_literal
}

string = { "\"" ~ string_content* ~ "\"" }

binding = { identifier ~ "=" ~ expr ~ ";" }
attrset = { "{" ~ binding* ~ "}" }

list = { "[" ~ expr* ~ "]" }

nix_file = { SOI ~ expr ~ EOI }
