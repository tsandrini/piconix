// ---------------------------------------------------------------------
// Main rules
// ---------------------------------------------------------------------
source = { SOI ~ WHITESPACE* ~ nix_expression ~ WHITESPACE* ~ EOI }
nix_expression = { term }
// binding = { identifier ~ WHITESPACE* ~ "=" ~ WHITESPACE* ~ nix_expression ~ WHITESPACE* ~ ";" }

identifier_str      = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_" | "-")* }
identifier = { identifier_str ~ ("." ~ identifier_str)* }
binding = { identifier ~ "=" ~ nix_expression ~ ";" }

// ---------------------------------------------------------------------
// Terms - The building blocks of an expression.
// ---------------------------------------------------------------------
term = {
    list |
    attrset |
    string |
    null |
    boolean |
    float |      // float must be checked before integer
    integer |
    identifier
}

// ---------------------------------------------------------------------
// Complex Types
// ---------------------------------------------------------------------
list    = { "[" ~ (WHITESPACE* ~ nix_expression ~ WHITESPACE*)* ~ "]" }
attrset = { "{" ~ (WHITESPACE* ~ binding ~ WHITESPACE*)* ~ "}" }

// ---------------------------------------------------------------------
// Literals & Identifiers
// ---------------------------------------------------------------------
integer         = @{ ("-")? ~ ASCII_DIGIT+ }
float           = @{ ("-")? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
boolean         = @{ "true" | "false" }
null            = @{ "null" }

// ---------------------------------------------------------------------
// String literal + escaping
// ---------------------------------------------------------------------
string                = { "\"" ~ string_content* ~ "\"" }
interpolation         = { "${" ~ nix_expression ~ "}" }
escaped_quote         = @{ "\\" ~ "\"" }
escaped_interpolation = @{ "''${" }
string_literal_part   = @{ ( !("\"" | "$" | "\\" | "'") ~ ANY )+ }
dollar_literal        = @{ "$" ~ !("{") }
single_quote_literal  = @{ "'" ~ !"'" }
string_content = {
    escaped_quote |
    escaped_interpolation |
    interpolation |
    string_literal_part |
    dollar_literal |
    single_quote_literal
}

// ---------------------------------------------------------------------
// Whitespace and comments
// ---------------------------------------------------------------------
COMMENT    = _{ "#" ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{ " " | "\t" | NEWLINE | COMMENT }
